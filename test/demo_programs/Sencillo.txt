begin-world mimundo

  Start at 1 1 heading north;
  Basket of capacity 99999999;

  Boolean mytrue with initial value true;
  World 11 11;


  Wall east from 2 1 to 11 1;
  Wall north from 11 1 to 11 11;
  Wall west from 11 11 to 1 11;
  Wall south from 1 11 to 1 3;
  Wall east from 2 3 to 7 3;
  Wall east from 9 3 to 10 3;
  Wall east from 2 5 to 5 5;
  Wall east from 7 5 to 9 5;
  Wall east from 3 7 to 7 7;
  Wall east from 3 9 to 5 9;
  Wall east from 7 9 to 7 9;
  Wall north from 7 4 to 7 9;
  Wall north from 9 5 to 9 5;
  Wall north from 9 7 to 9 11;
  Wall north from 3 9 to 3 11;

  Object-type o of color red;
  Object-type llegada of color green;

  Place 99999 of o in basket;

  --Flags:
  Place 1 of llegada at 2 4;

  Goal llegadaF is 1 llegada objects in Basket;

  Final goal is llegadaF;

end-world

begin-task Sencillo on mimundo

  -- Laberinto sencillo donde willy es capaz de encontrar el camino

  define headNorth as while not looking-north do turn-left;
  define headEast as while not looking-east do turn-left;
  define headWest as while not looking-west do turn-left;
  define headSouth as while not looking-south do turn-left;

  define backtracking as
    begin
      if llegadaF then terminate;
      if found(llegada) then pick llegada;
      if not found(o) then 
        begin
          drop o;
          headNorth;
          if front-clear then 
            begin
              move;
              backtracking;
              headSouth;
              move;
            end
            headNorth;
            if right-clear then
              begin
                headEast;
                move;
                backtracking;
                headWest;
                move;
              end  
            headNorth;
            if left-clear then
              begin
                headWest;
                move;
                backtracking;
                headEast;
                move;
              end  
            headSouth;
            if front-clear then 
            begin
              move;
              backtracking;
              headNorth;
              move;
            end
        end 
    end
  
  backtracking;

end-task