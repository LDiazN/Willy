begin-world mimundo
  World 10 10;
  Start at 5 5 heading south;
  Basket of capacity 100;


  Boolean searching with initial value false;
  
  Object-type redSpot of color red;
  Object-type greenSpot of color green;
  Object-type cubeRed of color red;
  Object-type ballGreen of color green;


  Place 1 of redSpot at 1 1;
  Place 1 of greenSpot at 1 10;

  Place 1 of ballGreen at 5 5;
  Place 1 of ballGreen at 2 3;
  Place 1 of ballGreen at 8 2;

  
  Place 1 of cubeRed at 2 5;
  Place 1 of cubeRed at 3 2;
  Place 1 of cubeRed at 2 8;

  Goal allRed is 3 cubeRed objects at 1 1;
  Goal allGreen is 3 ballGreen objects at 1 10;

  Final goal is allGreen and allRed;

end-world


begin-task classify on mimundo

  
  -- En este programa intentaremos que willy recoja 
  -- todas las bolas verdes y los ponga en la zona verde,
  -- y lo mismo con las bolas rojas en la zona roja.

  define headNorth as while not looking-north do turn-left;
  define headEast as while not looking-east do turn-left;
  define headWest as while not looking-west do turn-left;
  define headSouth as while not looking-south do turn-left;

  define goToSouthAndGoBack as
    begin
      headSouth;
      if found(redSpot) and carrying(cubeRed) then
        drop cubeRed;
      if front-clear then
        begin
          move;
          goToSouthAndGoBack;
          headNorth;
          move;
        end
    end

  define goToRedSpot1 as
    begin
      headWest;
      if front-clear then 
        begin
          move;
          goToRedSpot1;
          headEast;
          move;
        end
      else
        goToSouthAndGoBack;
    end

  define goToRedSpot as 
    begin
      if looking-north then 
        begin
          goToRedSpot1;
          headNorth;
        end
      if looking-south then 
        begin
          goToRedSpot1;
          headSouth;
        end
      if looking-west then 
        begin
          goToRedSpot1;
          headWest;
        end
      if looking-east then 
        begin
          goToRedSpot1;
          headEast;
        end
    end

  define goToNorthAndGoBack as
    begin
      headNorth;
      if found(greenSpot) and carrying(ballGreen) then
        drop ballGreen;
      if front-clear then
        begin
          move;
          goToNorthAndGoBack;
          headSouth;
          move;
        end
    end

  define goToGreenSpot1 as
    begin
      headWest;
      if front-clear then 
        begin
          move;
          goToGreenSpot1;
          headEast;
          move;
        end
      else
        goToNorthAndGoBack;
    end

  define goToGreenSpot as 
    begin
      if looking-north then 
        begin
          goToGreenSpot1;
          headNorth;
        end
      if looking-south then 
        begin
          goToGreenSpot1;
          headSouth;
        end
      if looking-west then 
        begin
          goToGreenSpot1;
          headWest;
        end
      if looking-east then 
        begin
          goToGreenSpot1;
          headEast;
        end
    end

  --Empezamos moviendo a willy hacia la esquina superior izquierda
  headNorth;
  while front-clear do move;
  headWest;
  while front-clear do move;
  
  --Ahora escaneamos en busca de objetos
  headEast;
  while front-clear do
    begin 
      if found(ballGreen) then
        begin
          pick ballGreen;
          goToGreenSpot;
        end
      if found(cubeRed) then
        begin
          pick cubeRed;
          goToRedSpot;
        end
      
      move;
      if not front-clear and looking-east and right-clear then
        begin
          headSouth;
          move;
          headWest;
        end;
      
      if not front-clear and looking-west and left-clear then 
        begin
          headSouth;
          move;
          headEast;
        end
    end 


end-task