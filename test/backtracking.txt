begin-world mimundo

  Start at 1 1 heading north;
  Basket of capacity 99999999;

  Boolean mytrue with initial value true;
  World 10 10;
  Wall north from 2 2 to 2 3;
  Wall north from 1 2 to 1 3;
  Wall north from 5 1 to 5 3;
  Wall south from 8 10 to 8 7;
  Wall south from 2 10 to 2 5;
  Wall west from 10 5 to 5 5;

  Object-type migaja of color red;
  Object-type flag of color green;

  Place 9999999 of migaja in basket;

  --Flags:
  Place 1 of flag at 10 10;
  Place 1 of flag at 3 3;
  Place 1 of flag at 1 8;
  Place 1 of flag at 8 3;

  Goal fourFlagz is 4 flag objects in Basket;

  Final goal is fourFlagz;

end-world


begin-task backtracking on mimundo

  --En este programa, nuestro héroe Willy deberá conseguir las 4 flags
  --en el laberinto del fauno para ganar. Usará un rastro de migajas de
  --pan para saber de donde viene y poder volver a sus inicios.

  define headNorth as while not looking-north do turn-left;
  define headEast as while not looking-east do turn-left;
  define headWest as while not looking-west do turn-left;
  define headSouth as while not looking-south do turn-left;

  define backtracking as
    begin
      if found(flag) then pick flag;
      if not found(migaja) then 
        begin
          drop migaja;
          headNorth;
          if front-clear then 
            begin
              move;
              backtracking;
              headSouth;
              move;
            end
            headNorth;
            if right-clear then
              begin
                headEast;
                move;
                backtracking;
                headWest;
                move;
              end  
            headNorth;
            if left-clear then
              begin
                headWest;
                move;
                backtracking;
                headEast;
                move;
              end  
            headSouth;
            if front-clear then 
            begin
              move;
              backtracking;
              headNorth;
              move;
            end
        end 
    end
  
  backtracking;

end-task